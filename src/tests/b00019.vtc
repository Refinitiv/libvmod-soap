varnishtest "SOAP OO: valid xpath header and body from object / response"

server s1 {
	rxreq
	txresp -body {<?xml version="1.0" encoding="utf-8"?>
		<Envelope xmlns="http://schemas.xmlsoap.org/soap/envelope/">
		<Header>
		<userIdentity xmlns="http://schemas.reuters.com/ns/2005/08/infrastructure/tornado_soap">
		<UUID xmlns="http://schemas.reuters.com/ns/2007/10/cp/user_identity">lenin</UUID>
		</userIdentity>
		</Header><Body>
		<Test xmlns="http://schemas.reuters.com/mytest"/>
		</Body></Envelope>
	}
} -start

varnish v1 -vcl+backend {
	import ${vmod_soap};
	import std;

	sub vcl_init {
		new body_resp = soap.parser(source=resp_body);
		body_resp.add_namespace("ui", "http://schemas.reuters.com/ns/2005/08/infrastructure/tornado_soap");
		body_resp.add_namespace("uuid", "http://schemas.reuters.com/ns/2007/10/cp/user_identity");
	}

	sub vcl_recv {
		return (pass);
	}

	sub vcl_deliver {
		set resp.http.soap-uuid = body_resp.header_xpath(
		    "ui:userIdentity/uuid:UUID");
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.bodylen == 398
	expect resp.http.soap-uuid == lenin
} -run
