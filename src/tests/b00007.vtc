varnishtest "SOAP: Body XPATH is not found / or empty."

server s1 {
        rxreq
        expect req.http.soap-uuid == "lenin"
        txresp
} -start

varnish v1 -vcl+backend {
    import ${vmod_soap};

    sub vcl_init
    {
            soap.add_namespace("ui", "http://schemas.reuters.com/ns/2005/08/infrastructure/tornado_soap");
            soap.add_namespace("uuid", "http://schemas.reuters.com/ns/2007/10/cp/user_identity");
    }

    sub vcl_recv
    {
            set req.http.soap-uuid = soap.xpath_body("ui:userIdentity/uuid:UUID");
            if(!req.http.soap-uuid || req.http.soap-uuid == "") {
                    soap.cleanup();
                    return (synth(400, "SOAP UUID is mandatory"));
            }
            soap.cleanup();
    }
} -start

client c1 {
        timeout 8
        txreq -req POST -body {<?xml version="1.0" encoding="utf-8"?>
                <Envelope xmlns="http://schemas.xmlsoap.org/soap/envelope/">
                <Header>
                </Header><Body>
                <Test xmlns="http://schemas.reuters.com/mytest"/>
                <userIdentity xmlns="http://schemas.reuters.com/ns/2005/08/infrastructure/tornado_soap">
                <UUID xmlns="http://schemas.reuters.com/ns/2007/10/cp/user_identity"></UUID>
                </userIdentity>
                </Body></Envelope>
        }
        rxresp
        expect resp.status == 400

        txreq -req POST -body {<?xml version="1.0" encoding="utf-8"?>
                <Envelope xmlns="http://schemas.xmlsoap.org/soap/envelope/">
                <Header>
                </Header><Body>
                <Test xmlns="http://schemas.reuters.com/mytest"/>
                <userIdentity xmlns="http://schemas.reuters.com/ns/2005/08/infrastructure/tornado_soap">
                <UUID xmlns="http://schemas.reuters.com/ns/2007/10/cp/user_identity">lenin</UUID>
                </userIdentity>
                </Body></Envelope>
        }
        rxresp
        expect resp.status == 200

        txreq -req POST -body {<?xml version="1.0" encoding="utf-8"?>
                <Envelope xmlns="http://schemas.xmlsoap.org/soap/envelope/">
                <Header>
                </Header><Body>
                <Test xmlns="http://schemas.reuters.com/mytest"/>
                <userIdentity xmlns="http://schemas.reuters.com/ns/2005/08/infrastructure/tornado_soap"></userIdentity>
                </Body></Envelope>
        }
        rxresp
        expect resp.status == 400
} -run
