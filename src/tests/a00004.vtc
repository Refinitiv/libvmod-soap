varnishtest "SOAP: Envelope contains element different than Header or Body."

server s1 {
        rxreq
        expect req.http.soap-ws-ns == "http://schemas.reuters.com/mytest"
        expect req.http.content-length == 198
        txresp -status 200
} -start

varnish v1 -vcl+backend {
    import ${vmod_soap};
    import std;

    sub vcl_recv
    {
            std.cache_req_body(1K);
            if(soap.is_valid()) {
                    set req.http.soap-ws-ns = soap.action_namespace();
            }
            else {
                    return (synth(400, "Wrong SOAP message"));
            }
    }
    
} -start

client c1 {
        timeout 8
        txreq -req POST -body {<?xml version="1.0" encoding="utf-8"?>
        <Envelope xmlns="http://schemas.xmlsoap.org/soap/envelope/">
        <Foo><Test xmlns="http://schemas.reuters.com/mytest"/></Foo>
        </Envelope>}
        rxresp
        expect resp.status == 400

        txreq -req POST -body {<?xml version="1.0" encoding="utf-8"?>
        <Envelope xmlns="http://schemas.xmlsoap.org/soap/envelope/">
        <Foo></Foo>
        <Body><Test xmlns="http://schemas.reuters.com/mytest"/></Body>
        </Envelope>}
        rxresp
        expect resp.status == 400

        txreq -req POST -body {<?xml version="1.0" encoding="utf-8"?>
        <Envelope xmlns="http://schemas.xmlsoap.org/soap/envelope/">
        <Header></Header>
        <Foo><Test xmlns="http://schemas.reuters.com/mytest"/></Foo>
        </Envelope>}
        rxresp
        expect resp.status == 400

        txreq -req POST -body {<?xml version="1.0" encoding="utf-8"?>
        <Envelope xmlns="http://schemas.xmlsoap.org/soap/envelope/">
        <Body><Test xmlns="http://schemas.reuters.com/mytest"/></Body>
        </Envelope>}
        rxresp
        expect resp.status == 200
} -run
